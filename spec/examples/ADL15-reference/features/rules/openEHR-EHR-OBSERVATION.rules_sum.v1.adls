archetype (adl_version=1.5.1)
	openEHR-EHR-OBSERVATION.rules_sum.v1

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"18/05/2004">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Test sum statement in rules section">
			copyright = <"© 2011 openEHR Foundation">
		>
	>
	other_details = <
		["regression"] = <"PASS">
	>

definition
	OBSERVATION[id1] matches {	-- Apgar score
		data matches {
			HISTORY[id3] matches {	-- history
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[id4] occurrences matches {0..1} matches {	-- 1 minute
						offset matches {
							DV_DURATION[id38] matches {
								value matches {|PT1M|}
							}
						}
						data matches {
							ITEM_LIST[id2] matches {	-- structure
								items cardinality matches {1..6; ordered} matches {
									ELEMENT[id10] occurrences matches {0..1} matches {	-- Respiratory effort
										value matches {
											DV_ORDINAL[id39] matches {
												[value, symbol] matches {
													[{0}, {[local::at10]}], 	-- Absent
													[{1}, {[local::at11]}], 	-- Weak or irregular
													[{2}, {[local::at12]}]  	-- Normal
												}
											}
										}
									}
									ELEMENT[id6] occurrences matches {0..1} matches {	-- Heart Rate
										value matches {
											DV_ORDINAL[id40] matches {
												[value, symbol] matches {
													[{0}, {[local::at6]}], 	-- Absent
													[{1}, {[local::at7]}], 	-- <100 beats per minute
													[{2}, {[local::at8]}]  	-- â‰¥100 beats per minute
												}
											}
										}
									}
									ELEMENT[id14] occurrences matches {0..1} matches {	-- Muscle tone
										value matches {
											DV_ORDINAL[id41] matches {
												[value, symbol] matches {
													[{0}, {[local::at14]}], 	-- Limp or flaccid
													[{1}, {[local::at15]}], 	-- Reduced tone
													[{2}, {[local::at16]}]  	-- Normal tone
												}
											}
										}
									}
									ELEMENT[id18] occurrences matches {0..1} matches {	-- Reflex irritability
										value matches {
											DV_ORDINAL[id42] matches {
												[value, symbol] matches {
													[{0}, {[local::at18]}], 	-- No response
													[{1}, {[local::at19]}], 	-- Reduced response
													[{2}, {[local::at20]}]  	-- Normal response
												}
											}
										}
									}
									ELEMENT[id22] occurrences matches {0..1} matches {	-- Skin colour
										value matches {
											DV_ORDINAL[id43] matches {
												[value, symbol] matches {
													[{0}, {[local::at22]}], 	-- Completely blue
													[{1}, {[local::at23]}], 	-- Body pink; extremities blue
													[{2}, {[local::at24]}]  	-- Completely pink
												}
											}
										}
									}
									ELEMENT[id26] occurrences matches {0..1} matches {	-- Total
										value matches {
											DV_COUNT[id44] matches {
												magnitude matches {|0..10|}
											}
										}
									}
								}
							}
						}
					}
					POINT_EVENT[id27] occurrences matches {0..1} matches {	-- 2 minute
						offset matches {
							DV_DURATION[id45] matches {
								value matches {|PT2M|}
							}
						}
						data matches {
							use_node ITEM_LIST[id46] /data[id3]/events[id4]/data[id2]	-- /data[history]/events[1 minute]/data[structure]
						}
					}
					POINT_EVENT[id28] occurrences matches {0..1} matches {	-- 3 minute
						offset matches {
							DV_DURATION[id47] matches {
								value matches {|PT3M|}
							}
						}
						data matches {
							use_node ITEM_LIST[id48] /data[id3]/events[id4]/data[id2]	-- /data[history]/events[1 minute]/data[structure]
						}
					}
					POINT_EVENT[id29] occurrences matches {0..1} matches {	-- 5 minute
						offset matches {
							DV_DURATION[id49] matches {
								value matches {|PT5M|}
							}
						}
						data matches {
							use_node ITEM_LIST[id50] /data[id3]/events[id4]/data[id2]	-- /data[history]/events[1 minute]/data[structure]
						}
					}
					POINT_EVENT[id32] occurrences matches {0..1} matches {	-- 10 minute
						offset matches {
							DV_DURATION[id51] matches {
								value matches {|PT10M|}
							}
						}
						data matches {
							use_node ITEM_LIST[id52] /data[id3]/events[id4]/data[id2]	-- /data[history]/events[1 minute]/data[structure]
						}
					}
					EVENT[id38] occurrences matches {0..*} matches {	-- Any event
						data matches {
							use_node ITEM_LIST[id53] /data[id3]/events[id4]/data[id2]	-- /data[history]/events[1 minute]/data[structure]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_LIST[id30] matches {	-- List
				items matches {
					ELEMENT[id31] occurrences matches {0..1} matches {	-- Notes on measurement
						value matches {
							DV_TEXT[id54] matches {*}
						}
					}
				}
			}
		}
	}

invariant
	score_sum: 
		/data[id3]/events[id4]/data[id2]/items[id26]/value[id44]/magnitude =
		/data[id3]/events[id4]/data[id2]/items[id6]/value[id40]/value + 
		/data[id3]/events[id4]/data[id2]/items[id10]/value[id39]/value +
		/data[id3]/events[id4]/data[id2]/items[id14]/value[id41]/value + 
		/data[id3]/events[id4]/data[id2]/items[id18]/value[id42]/value + 
		/data[id3]/events[id4]/data[id2]/items[id22]/value[id43]/value


ontology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Apgar score">
				description = <"Clinical score derived from assessment of respiratory effort, heart rate, reflex irritability, muscle tone and skin colour.">
			>
			["id4"] = <
				text = <"1 minute">
				description = <"Apgar score 1 minute after birth.">
			>
			["id6"] = <
				text = <"Heart Rate">
				description = <"Recording of the infant's heart rate.">
			>
			["id10"] = <
				text = <"Respiratory effort">
				description = <"Observation of the infant's respiratory effort.">
			>
			["id14"] = <
				text = <"Muscle tone">
				description = <"Observation of the infant's muscle tone.">
			>
			["id18"] = <
				text = <"Reflex irritability">
				description = <"Observation of the response of the infant to an irritant stimulation, for example, suctioning the oropharynx and nares with a soft rubber catheter.">
			>
			["id22"] = <
				text = <"Skin colour">
				description = <"Observation of the skin colour of the infant.">
			>
			["id26"] = <
				text = <"Total">
				description = <"The sum of the 5 ordinal scores for each component parameter.">
			>
			["id27"] = <
				text = <"2 minute">
				description = <"Apgar score 2 minutes after birth.">
			>
			["id28"] = <
				text = <"3 minute">
				description = <"Apgar score 3 minutes after birth.">
			>
			["id29"] = <
				text = <"5 minute">
				description = <"Apgar score 5 minutes after birth.">
			>
			["id31"] = <
				text = <"Notes on measurement">
				description = <"Notes on measurement of the Apgar score.">
			>
			["id32"] = <
				text = <"10 minute">
				description = <"Apgar score 10 minutes after birth.">
			>
			["id38"] = <
				text = <"Any event">
				description = <"Apgar score at any additional time, as required.">
			>
			["at6"] = <
				text = <"Absent">
				description = <"No heart beat is seen, felt or heard.">
			>
			["at7"] = <
				text = <"<100 beats per minute">
				description = <"Heart rate less than 100 beats per minute.">
			>
			["at8"] = <
				text = <"â‰¥100 beats per minute">
				description = <"Heart rate greater than or equal to 100 beats per minute.">
			>
			["at10"] = <
				text = <"Absent">
				description = <"No effort to breath.">
			>
			["at11"] = <
				text = <"Weak or irregular">
				description = <"Some effort to breath, moving chest.">
			>
			["at12"] = <
				text = <"Normal">
				description = <"Breathing normally or crying.">
			>
			["at14"] = <
				text = <"Limp or flaccid">
				description = <"No spontaneous movement.">
			>
			["at15"] = <
				text = <"Reduced tone">
				description = <"Some flexion of extremities.">
			>
			["at16"] = <
				text = <"Normal tone">
				description = <"Normal, vigorous movements.">
			>
			["at18"] = <
				text = <"No response">
				description = <"No response to stimulation.">
			>
			["at19"] = <
				text = <"Reduced response">
				description = <"Grimace or feeble cry when stimulated.">
			>
			["at20"] = <
				text = <"Normal response">
				description = <"Grimace, sneeze, cough or pulls away when stimulated.">
			>
			["at22"] = <
				text = <"Completely blue">
				description = <"Body and extremities are blue.">
			>
			["at23"] = <
				text = <"Body pink; extremities blue">
				description = <"Body is pink; extremities are blue.">
			>
			["at24"] = <
				text = <"Completely pink">
				description = <"Body and extremities are pink; no cyanosis.">
			>
		>
	>
